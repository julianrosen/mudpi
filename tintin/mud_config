#nop Move multiple rooms at once
#config {speedwalk} {on}
#nop {#config {buffer size} {1000000}}


#nop Don't display connecting messages
#gag {#SESSION}
#gag {#TRYING}
#gag {Use ANSI Color?}
#gag {#ERROR: PROMPT ROW IS INSIDE THE SCROLLING REGION}

#nop Mudpi uses these strings to communicate with TinTin
#gag {^@^t}
#gag {@^p1}
#gag {@^p2}

#nop Enable static prompt
#alias {setttprompt}
{
    #prompt {~@^p1%%1}{%%1}{-2};
    #prompt {~@^p2%%1}{%%1}{-1};
    #delay {0.1} {#split};
    #delay {0.15} {#cr};
    #delay {0.1} {#screen clear bottom};
    #delay {0.1} {#screen clear input};
    #mudpi fixed;
}

#nop Disable static prompt
#alias {unsetttprompt}
{
    #unprompt {~@^p1%%1};
    #unprompt {~@^p2%%1};
    #nop {#split 0 0};
    #mudpi nonfixed;
    #screen clear bottom;
    #screen clear input;
    #delay {0.15} {#cr};
    #screen clear bottom;
    #screen clear input;
}

#nop Mud prefixes commands to TinTin with @^t
#action {^@^t %1|%2}
{
    #switch {"%1"}
    {
        #case "split"
        {
            #switch {"%2"}
            {
                #case "on" #split 0 0;
                #case "off" #unsplit;
            } 
        };
        #case "fixed"
        {
            #switch {"%2"}
            {
                #case "on" setttprompt;
                #case "off" unsetttprompt;
            }
        };
        #case "brief"
        {
            #switch {"%2"}
            {
                #case "on" {#mudpi brief};
                #case "off" {#mudpi nonbrief};
            }
        };
        #case "compact"
        {
            #switch {"%2"}
            {
                #case "on" {#mudpi compact};
                #case "off" {#mudpi noncompact};
            }
        };
    };
}

#nop When we receive a non-escaped output line, update the
#nop variable that controlls spacing of TinTin input lines
#event {RECEIVED LINE} {#regexp {%1} {^@^} {#nop} {#shownoutput}}

#nop Better than nothing...
#event {UNKNOWN COMMAND} {#shownoutput}

#nop Terminate session upon disconnect
#event {SESSION DISCONNECTED} {#end}

#nop This string is entered at ANSI prompt. It's how the MUD;
#nop learns that the custom TinTin client has connected.;
#event {SESSION CONNECTED} {tintin}

#nop On screen resize, clear those weird lines that appear and redraw prompt
#event {SCREEN RESIZE} {#screen clear input;#screen clear bottom;#cr}

#nop Unstable version only
#nop #showme This is the unstable version of Mudpi

#nop Connect to the mud
#ses mud_pi localhost 20495
